rule "Init"
when
	System started
then
  AtHome.sendCommand(ON)
end

rule "Turn off heating when leaving the home"
when
  Item AtHome changed to OFF
then
  logInfo("presence", "Turning off bathroom floor heating because leaving home")
  HeatingBathroom_State.sendCommand(OFF)

  logInfo("presence", "Turning on vacation mode on thermostats because not at home")
  HeatingOffice_1_ActiveProfile.sendCommand(2)
  HeatingBedroom_1_ActiveProfile.sendCommand(2)
end

rule "Turn on heating when entering home"
when
  Item AtHome changed from OFF to ON
then
  logInfo("presence", "Turning off vacation mode on thermostats because not at home")
  HeatingOffice_1_ActiveProfile.sendCommand(1)
  HeatingBedroom_1_ActiveProfile.sendCommand(1)
end

rule "Turn off bathroom floor heating when not at home"
when
  Item HeatingBathroom_State changed to ON
then
  if(AtHome.state == OFF) {
    logInfo("presence", "Turning off bathroom floor heating because not at home")
    HeatingBathroom_State.sendCommand(OFF)
  }
end

rule "Switch home presence based on mobile phones\' presence in network"
when
	Item PresenceAnastasiia_Online changed or Item Pavlo_AtHome changed
then
	if((PresenceAnastasiia_Online.state == ON) || (Pavlo_AtHome.state == ON)) {
		logInfo("presence", "At least one phone is at home.");
		AtHome.sendCommand(ON);
	}
	else {
		val hours = Integer::parseInt(String::format("%1$tk", new java.util.Date));
		// activate rule only during "daytime"
		if((hours < 22) && (hours > 6)) {
			logInfo("presence", "Both phones have left home.");
			AtHome.sendCommand(OFF);
		}
	}
end
